#!/usr/bin/env perl
use lib 'lib';
use Mojolicious::Lite; ## strict, warnings, utf8 and Perl 5.10

use SylSpace::Model::Utils qw(_getvar);
my $vardir=_getvar();

plugin 'Mojolyst' => {controllers => 'SylSpace::Controller'};
plugin 'RenderFile';
plugin 'browser_detect';

my $dmnm= (glob("$vardir/domainname=*"))[0];

(defined($dmnm)) or die "please set a (virtual) domainname in $vardir/domainname=xx.yy";

$dmnm =~ s{^\Q$vardir\E/domainname\=}{};
$ENV{MOJO_DOMAINNAME}= $dmnm;

helper this_domain => sub { $dmnm };

app->log->debug("may not work except under hypnotoad")
  unless defined $ENV{HYPNOTOAD_APP};

## $0 = MOJO_EXE = /Users/ivo/syllabus.space/syllabus.space/SylSpace
(defined($ENV{MOJO_EXE})) or die "very weird running environment without ENV{MOJO_EXE}.";

use FindBin;
use Socket;

(my $pgmname= $0) =~ s{.*/}{};  ## usually SylSpace

$ENV{SYLSPACE_sitepath}= $FindBin::Bin;    ## /Users/ivo/syllabus.space/syllabus.space/

($ENV{SYLSPACE_appname}= $0) =~ s{.*/}{};  ## SylSpace, from executable command

## remove trailing port number
($ENV{SYLSPACE_sitename}= $ENV{MOJO_DOMAINNAME}) =~ s/:.*//g;  ## syllabus.test, from Mojo

($ENV{MOJO_DOMAINNAME} eq 'localhost') and die "\nyou cannot use localhost, because we need subdomains for testing.  please use lvh.me instead.\n";

app->log->debug("SylSpace: dmnm='$ENV{MOJO_DOMAINNAME}'");
my $inetaton= inet_aton($ENV{MOJO_DOMAINNAME});
app->log->debug("SylSpace: ...$inetaton");

$ENV{SYLSPACE_siteip}= inet_ntoa( $inetaton );  ## 127.0.0.1
$ENV{SYLSPACE_onlocalhost} = ($ENV{SYLSPACE_siteip} eq '127.0.0.1');

my $configfname= "$ENV{SYLSPACE_sitepath}/$pgmname-Secrets.conf";

if ($ENV{SYLSPACE_onlocalhost} and $ENV{SYLSPACE_sitename} !~ /lvh[.]me$/) {
  ($ENV{SYLSPACE_sitename} eq 'syllabus.test') or die "please test only on syllabus.test or lvh.me";
  my $etclines= `grep syllabus.test /etc/hosts`;
  ($etclines =~ /corpfin.syllabus.test/m) or die "\n\nFor local testing, please add at the end of /etc/hosts the following: \n\n127.0.0.1 syllabus.test auth.syllabus.test corpfin.syllabus.test\n\n\n";
} else {
  (-e $configfname) or die "you need a $configfname config file in non-local operation\n"
}

################

$ENV{SYLSPACE_siteacolor}='AntiqueWhite'; $ENV{SYLSPACE_jumboacolor}='darkgreen';
$ENV{SYLSPACE_siteicolor}=$ENV{SYLSPACE_sitescolor}='white';
$ENV{SYLSPACE_jumboscolor}='darkblue'; $ENV{SYLSPACE_jumboicolor}='darkred';


################################################################################################################################
## unfortunately, you have to be connected to the correct diretory; I think Mojolyst does not work otherwise
################################################################################################################################

if (-e $configfname) {

  app->log->debug("$0: good: your authentication secrets is $configfname");

  ##  # working:
  ##  plugin 'Config';
  ##  my $credentials = app->plugin('Config')->{oauth};

  my $credentials= plugin 'Config' => { file => $configfname };

  (defined($credentials)) or die "internal error: I cannot read the credentials information from the config file $configfname\n";
  (defined($credentials->{oauth})) or die "internal error: I cannot read the oauth field information from the config file $configfname\n";

  if (my $google = $credentials->{oauth}->{google}) {
    my ($key, $secret) = @$google;

    app->log->debug("[enabling google authentication]");

    plugin 'OAuth2' => {
	google  => {
		key	=> $key,
		secret	=> $secret,
		authorize_url	=> "https://accounts.google.com/o/oauth2/v2/auth?response_type=code",
		token_url	=> "https://www.googleapis.com/oauth2/v4/token",
	},
    };


    plugin 'Web::Auth' => (
			   module      => 'Google',
			   key         => $key,
			   secret      => $secret,
			   scope       => 'email',
			   on_error	=> \&SylSpace::Controller::AuthAuthenticator::googlecb,
			   on_finished => \&SylSpace::Controller::AuthAuthenticator::google,
			  );

  }

  if (my $github = $credentials->{oauth}->{github}) {
    my ($key, $secret) = @$github;

    app->log->debug("[enabling github authentication]");

    plugin 'Web::Auth' => (
			   module      => 'Github',
			   key         => $key,
			   secret      => $secret,
			   on_finished => \&SylSpace::Controller::AuthAuthenticator::github,
			  );
  }

  if (my $facebook = $credentials->{oauth}->{facebook}) {
    my ($key, $secret) = @$facebook;

    app->log->debug("[enabling facebook authentication]");

    plugin 'Web::Auth' => (
			   module      => 'Facebook',
			   key         => $key,
			   secret      => $secret,
			   scope       => 'email',
			   on_finished => \&SylSpace::Controller::AuthAuthenticator::facebook
			  );
  }

  $ENV{SYLSPACE_haveoauth}= 1;
}


################################################################################################################################
## SHOULD THESE BE REPLACED IN FAVOR OF %ENV variables?
## http://user.course.$ENV{SYLSPACE_sitename} -> $ENV{SYLSPACE_sitename}
## http://$ENV{'sitename'} -> $ENV{'sitename'}

# *Mojo::URL::domainonly = sub {
#   return $ENV{SYLSPACE_sitename};
# };

helper domainport => sub {
  my $url  = shift->req->url->to_abs;
  my $port = $url->port;
  defined($ENV{SYLSPACE_sitename}) or die "internal error: no site ($ENV{SYLSPACE_sitename})";
  (defined($port)) or return $ENV{SYLSPACE_sitename};
  return "$ENV{SYLSPACE_sitename}:$port";
};

helper auth_path => sub {
  my ($c, $path) = @_;
  $c->req->url->to_abs->clone
    ->path($path)
    ->host("auth.$ENV{SYLSPACE_sitename}")
    ->query(undef);
};
  

helper subdomain => sub {
  my $domain = shift->req->url->to_abs->host || 'unknown-fulldomain';
  ($domain =~ m/(.*)\.localhost$/) and return $1;
  my @f = split(/\./, $domain);
  return '' unless @f;  ## never
  splice @f, -2; #pop off tld and main site name
  return join '.', @f;
};

################################################################################################################################
use Perl6::Slurp;


my @pwsecret=slurp("$vardir/secrets.txt");
app->secrets( \@pwsecret );  ## the first one encodes, the others are usable; maybe rotate by day

#default hypnotoad to port 80, but allow configuration
app->config->{hypnotoad}{listen} //= ['http://*:80'];

use File::Glob ':bsd_glob';
my @users= (bsd_glob("$vardir/users/*\@*"));
((scalar @users) >= 1) or die "you need at least one known users in $vardir/users, not $#users\n";
my @courses=(scalar glob("$vardir/courses/*"));
((scalar @courses) >= 1) or die "you need at least one known course in $vardir/courses, not $#courses\n";

if ($ENV{'SYLSPACE_onlocalhost'}) {
  app->log->debug("$0: *** localhost mode only --- logging out may not work correctly");
}

app->log->debug("$0: Starting $ENV{SYLSPACE_appname} now on $ENV{MOJO_DOMAINNAME}");

app->sessions->cookie_domain( '.'.$ENV{SYLSPACE_sitename} );

app->start();
