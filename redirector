#! /usr/bin/env perl
use Mojolicious::Lite;
use lib app->home->child('lib')->to_string;
use lib app->home->child('local/lib/perl5')->to_string;

app->config({
  hypnotoad => {
    listen => [ 'http://*:80' ],
    pid_file => app->home->child('redirector.pid')
  }
});

my $redirect =  sub {
  my $c = shift;
  $c->res->code(301);

  #grab just the subdomain
  my @domain_parts = split /[.]/, $c->req->url->to_abs->host;
  my @dmn = splice @domain_parts, -2;
  my $subdomain = join '-', @domain_parts;
  unshift @dmn, $subdomain if $subdomain;

  $c->redirect_to(
    $c->req->url->to_abs
      ->port(undef)
      ->host(join '.', @dmn)
      ->scheme('https')
  )
};

any '/' => $redirect;
any '/*all' => $redirect;

app->start
